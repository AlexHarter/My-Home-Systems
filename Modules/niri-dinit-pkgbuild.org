* [[https://gitlab.archlinux.org/archlinux/packaging/packages/niri/-/blob/main/PKGBUILD?ref_type=heads][Arch pkgbuild]]
#+BEGIN_SRC bash
  # Maintainer: David Runge <dvzrv@archlinux.org>

  pkgname=niri
  pkgver=0.1.10
  pkgrel=1
  pkgdesc="A scrollable-tiling Wayland compositor"
  arch=(x86_64)
  url="https://github.com/YaLTeR/niri"
  license=(GPL-3.0-or-later)
  depends=(
    cairo
    gcc-libs
    glib2
    glibc
    libdisplay-info
    libinput
    libpipewire
    libxkbcommon
    mesa
    pango
    pixman
    seatd
    systemd-libs
    xdg-desktop-portal-impl
  )
  makedepends=(
    clang
    rust
  )
  optdepends=(
    'alacritty: a suggested GPU-accelerated terminal emulator'
    'bash: for niri-session script'
    'fuzzel: a suggested Wayland application launcher'
    'mako: a suggested Wayland notification daemon'
    'org.freedesktop.secrets: for apps to rely on secrets portal'
    'swaybg: a suggested Wayland wallpaper tool'
    'waybar: a suggested Wayland customizable desktop bar'
    'waylock: a suggested Wayland screen locker'
    'xdg-desktop-portal-gtk: a suggested XDG desktop portal'
    'xdg-desktop-portal-gnome: a XDG desktop portal required for screencasting'
  )
  # NOTE: linking issues with LTO enabled
  options=(!lto)
  source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
  sha512sums=('e596e7113f05fd160ff123225e76abf13ac1157d267e9d479ab2e52c38268b478eb7efe0d300a89bdab031f8efe35a28608eda8757671dce6de1d411c2bbdf93')
  b2sums=('46d671684019e875042680cf6062b2037ef682dfa5fe918b41fcd8228a806d3d9034321b911854bda58a7a496f4c368b68d54bfe7ec5be0fbbd4abf942eaf1d7')

  prepare() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  }

  build() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --features default
  }

  check() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --all --exclude niri-visual-tests --frozen
  }

  package() {
    cd $pkgname-$pkgver
    install -vDm 755 {target/release/$pkgname,resources/$pkgname-session} -t "$pkgdir/usr/bin/"
    install -vDm 644 resources/$pkgname{.service,-shutdown.target} -t "$pkgdir/usr/lib/systemd/user/"
    install -vDm 644 resources/$pkgname.desktop -t "$pkgdir/usr/share/wayland-sessions/"
    install -vDm 644 resources/$pkgname-portals.conf -t "$pkgdir/usr/share/xdg-desktop-portal/"
    install -vDm 644 resources/default-config.kdl README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  }
  
#+END_SRC
* [[https://github.com/chimera-linux/cports/blob/master/user/niri/template.py][Chimera cport]]
#+BEGIN_SRC python
  pkgname = "niri"
  pkgver = "0.1.10"
  pkgrel = 0
  build_style = "cargo"
  make_build_args = [
      "--no-default-features",
      "--features",
      "dbus,dinit,xdp-gnome-screencast",
  ]
  make_check_args = [*make_build_args]
  hostmakedepends = [
      "cargo-auditable",
      "pkgconf",
  ]
  makedepends = [
      "cairo-devel",
      "glib-devel",
      "libdisplay-info-devel",
      "libinput-devel",
      "libseat-devel",
      "libxkbcommon-devel",
      "mesa-devel",
      "pango-devel",
      "pipewire-devel",
      "pixman-devel",
      "rust-std",
      "udev-devel",
  ]
  depends = [
      "so:libEGL.so.1!libegl",
  ]
  pkgdesc = "Scrollable-tiling wayland compositor"
  maintainer = "psykose <alice@ayaya.dev>"
  license = "GPL-3.0-or-later"
  url = "https://github.com/YaLTeR/niri"
  source = f"{url}/archive/refs/tags/v{pkgver}.tar.gz"
  sha256 = "2a6e6c21b05c14498eb84e58853c0a4c9d96d4215b8aec53388e89266fccca1b"

  # TODO: dinit graphical user session service, --notify-fd, etc


  def install(self):
      self.install_bin(f"target/{self.profile().triplet}/release/niri")
      self.install_file("resources/niri.desktop", "usr/share/wayland-sessions")
      self.install_file(
	  "resources/niri-portals.conf", "usr/share/xdg-desktop-portal"
      )
#+END_SRC
* Artix-dinit system pkgbuild 
- [[https://github.com/YaLTeR/niri/wiki/Getting-Started#Packaging][Niri Packaging]]
- [[https://wiki.archlinux.org/title/PKGBUILD][Arch Wiki PKGBUILD]]
#+BEGIN_SRC bash :tangle PKGBUILD

  pkgname=niri
  pkgver=0.1.10
  pkgrel=1
  pkgdesc="A scrollable-tiling Wayland compositor"
  arch=(x86_64)
  url="https://github.com/YaLTeR/niri"
  license=(GPL-3.0-or-later)
  depends=(
    cairo
    dinit  
    gcc-libs
    glib2
    glibc
    libdisplay-info
    libinput
    libpipewire
    libxkbcommon
    mesa
    pango
    pixman
    seatd
    xdg-desktop-portal-impl
  )
  makedepends=(
    clang
    rust
  )
  optdepends=(
    'alacritty: a suggested GPU-accelerated terminal emulator'
    'bash: for niri-session script'
    'fuzzel: a suggested Wayland application launcher'
    'mako: a suggested Wayland notification daemon'
    'org.freedesktop.secrets: for apps to rely on secrets portal'
    'swaybg: a suggested Wayland wallpaper tool'
    'waybar: a suggested Wayland customizable desktop bar'
    'waylock: a suggested Wayland screen locker'
    'xdg-desktop-portal-gtk: a suggested XDG desktop portal'
    'xdg-desktop-portal-gnome: a XDG desktop portal required for screencasting'
  )
  # NOTE: linking issues with LTO enabled
  options=(!lto)
  source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
  sha512sums=('e596e7113f05fd160ff123225e76abf13ac1157d267e9d479ab2e52c38268b478eb7efe0d300a89bdab031f8efe35a28608eda8757671dce6de1d411c2bbdf93')
  b2sums=('46d671684019e875042680cf6062b2037ef682dfa5fe918b41fcd8228a806d3d9034321b911854bda58a7a496f4c368b68d54bfe7ec5be0fbbd4abf942eaf1d7')

  prepare() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  }

  build() {
    cd $pkgname-$pkgver
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --no-default-features --features dbus,dinit,xdp-gnome-screencast
  }

  check() {
    cd "$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo test --all --exclude niri-visual-tests --frozen
  }

  package() {
    cd $pkgname-$pkgver
    install -vDm 755 {target/release/$pkgname,resources/$pkgname-session} -t "$pkgdir/usr/bin/"
    install -vDm 644 resources/dinit/{$pkgname,$pkgname-shutdown} -t "$pkgdir/usr/lib/dinit.d/user/"
    install -vDm 644 resources/$pkgname.desktop -t "$pkgdir/usr/share/wayland-sessions/"
    install -vDm 644 resources/$pkgname-portals.conf -t "$pkgdir/usr/share/xdg-desktop-portal/"
    install -vDm 644 resources/default-config.kdl README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  }

#+END_SRC
